start: secure_unit | regular

regular: (attention_clause _SEP)? (building_number _SEP)? street secondary?
building_number . 1: NUMBER

street: (WORD _SEP*)+ type_indentifier?
secure_unit: pobox_clause (_SEP attention_clause)?
    | (attention_clause _SEP+)? pobox_clause
attention_clause: (attention_signifier _SEP+)? attention_subject
attention_signifier . 1: "ATTENTION" | "ATTN" | "C/O"
attention_subject: (WORD _SEP*)+
pobox_clause: pobox_signifier _SEP+ pobox_number
pobox_signifier . 1: "PO BOX" | "P O BOX" -> pobox_signifier
pobox_number: NUMBER

_SEP: COMMA | COLON | SPACE
COMMA: ","
COLON: ":"
SPACE: /\s/
WORD : /[\w&-]+-*/
NUMBER: /\d+/

?type_indentifier .1: STREET_TYPE
STREET_TYPE: "ALY" | "ANX" | "ARC" | "AVE" | "BYU" | "BCH" | "BND" | "BLFS" | "BTM" | "BLVD" | "BR" | "BRG" | "BRK"
	| "BG" | "BGS" | "BYP" | "CP" | "CYN" | "CSWY" | "CTR" | "CTRS" | "CIR" | "CIRS" | "CLF" | "CLFS" | "CLB" | "CMN"
	| "CMNS" | "COR" | "CORS" | "CRSE" | "CT" | "CTS" | "CV" | "CVS" | "CRK" | "CRES" | "CRST" | "XING" | "XRD" | "XRDS"
	| "CURV" | "DL" | "DM" | "DV" | "DR" | "DRS" | "EST" | "ESTS" | "EXPY" | "EXT" | "EXTS" | "FALL" | "FLS" | "FRY"
	| "FLD" | "FLDS" | "FLT" | "FLTS" | "FRD" | "FRDS" | "FRST" | "FRG" | "FRGS" | "FRK" | "FRKS" | "FT" | "FWY" | "GDN"
	| "GDNS" | "GTWY" | "GLN" | "GRN" | "GRNS" | "GRV" | "GRVS" | "HBR" | "HRBS" | "HVN" | "HTS" | "HWY" | "HL" | "HLS"
	| "HOLW" | "INLT" | "IS" | "ISS" | "ISLE" | "JCT" | "JCTS" | "KY" | "KYS" | "KNL" | "KNLS" | "LK" | "LKS" | "LAND"
	| "LNDG" | "LN" | "LGT" | "LGTS" | "LF" | "LCK" | "LCKS" | "LDG" | "LOOP" | "MALL" | "MNR" | "MNRS" | "MDW" | "MDWS"
	| "MEWS" | "ML" | "MLS" | "MSN" | "MTWY" | "MT" | "MTN" | "MTNS" | "NCK" | "ORCH" | "OVAL" | "OPAS" | "PARK"
	| "PKWY" | "PASS" | "PSGE" | "PATH" | "PIKE" | "PNE" | "PNES" | "PL" | "PLN" | "PLNS" | "PLZ" | "PT" | "PTS" | "PRT"
	| "PRTS" | "PR" | "RADL" | "RAMP" | "RNCH" | "RPD" | "RPDS" | "RST" | "RDG" | "RDGS" | "RIV" | "RD" | "RDS" | "RTE"
	| "ROW" | "RUE" | "SHL" | "SHLS" | "SHR" | "SHRS" | "SKWY" | "SPG" | "SPGS" | "SPUR" | "SQ" | "SQS" | "STA" | "STRA"
	| "STRM" | "ST" | "STS" | "SMT" | "TER" | "TRWY" | "TRCE" | "TRAK" | "TRFY" | "TRL" | "TRLR" | "TUNL" | "TPKE"
	| "UPAS" | "UN" | "UNS" | "VLY" | "VLYS" | "VIA" | "VW" | "VWS" | "VLG" | "VLGS" | "VL" | "VIS" | "WALK" | "WALL"
	| "WAY" | "WAYS" | "WL" | "WLS"



secondary: ((secondary_that_requires_range _SEP+ range) | (secondary_with_optional_range (_SEP+ range)?))
secondary_that_requires_range: SECONDARY_THAT_REQUIRES_RANGE
secondary_with_optional_range: SECONDARY_WITH_OPTIONAL_RANGE
SECONDARY_THAT_REQUIRES_RANGE: "APT" | "BLDG" | "DEPT" | "FL" | "HNGR" | "KEY" | "LOT" | "PIER" | "RM" | "SLIP" | "SPC"
	| "STOP" | "STE" | "TRLR" | "UNIT"
SECONDARY_WITH_OPTIONAL_RANGE: "BSMT" | "FRNT" | "LBBY" | "LOWR" | "OFC" | "PH" | "REAR" | "SIDE" | "UPPR"
range: (WORD _SEP*)+